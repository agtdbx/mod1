#version 430 core

layout (local_size_x = 8, local_size_y = 1, local_size_z = 1) in;

layout(r32f, binding = 0) uniform imageBuffer mapDensitiesBuffer;
layout(std430, binding = 1) readonly buffer DataBuffer {
	int gridBuffer[];
};

uniform float			smoothingRadius;
uniform float			smoothingRadius2;
uniform float			invSmoothingRadius;
uniform float			smoothingScale;
uniform float			waterMass;
uniform float			targetDensity;
uniform float			pressureMultiplier;
uniform int				mapBufferCellSize;
uniform int				mapBufferW;
uniform int				mapBufferIdHsize;
uniform int				mapBufferSize;
uniform int				gridW;
uniform int				gridH;
uniform int				gridD;
uniform int				idHsize;
uniform int				gridSize;
uniform samplerBuffer	positionsBuffer;


float	vecLength2(vec3 vec)
{
	return (vec.x * vec.x + vec.y * vec.y + vec.z * vec.z);
}


float	smoothingKernel(float dst2)
{
	if (dst2 > smoothingRadius2)
		return (0.0);

	return (pow(smoothingRadius - sqrt(dst2), 2) * smoothingScale);
}


float	calculateDensity(vec3 position)
{
	float	density, dst2, influence;
	int		px, py, pz, gx, gy, gz, gid, nbParticleInCell, waterId;
	vec3	positionWater;

	density = 0.0;
	px = int(position.x * invSmoothingRadius);
	py = int(position.y * invSmoothingRadius);
	pz = int(position.z * invSmoothingRadius);

	for (int cx = -1; cx <= 1; cx++)
	{
		gx = px + cx;
		if (gx < 0 || gx >= gridW)
			continue;

		for (int cy = -1; cy <= 1; cy++)
		{
			gy = py + cy;
			if (gy < 0 || gy >= gridH)
				continue;

			for (int cz = -1; cz <= 1; cz++)
			{
				gz = pz + cz;
				if (gz < 0 || gz >= gridD)
					continue;

				gid = gx + gz * gridW + gy * idHsize;
				nbParticleInCell = gridBuffer[gid];
				for (int i = 0; i < nbParticleInCell; i++)
				{
					gid += gridSize;
					waterId = gridBuffer[gid];

					positionWater = texelFetch(positionsBuffer, waterId).rgb;

					dst2 = vecLength2(positionWater - position);
					influence = smoothingKernel(dst2);
					density += waterMass * influence;
				}
			}
		}
	}

	return (density);
}


void main()
{
	int		cellIndex, idXZ, x, y, z;
	float	density, pressure;
	vec3	position;

	cellIndex = int(gl_GlobalInvocationID.x);
	if (cellIndex >= mapBufferSize || mapBufferIdHsize == 0 || mapBufferW == 0)
		return ;

	// Get cell pos in grid with cellIndex
	y = int(cellIndex / mapBufferIdHsize);
	idXZ = cellIndex - (y * mapBufferIdHsize);
	z = int(idXZ / mapBufferW);
	x = idXZ - (z * mapBufferW);

	// Get cell world position
	position = vec3(x, y, z) * mapBufferCellSize;
	density = calculateDensity(position);

	// Write the values to the buffers
	imageStore(mapDensitiesBuffer, cellIndex, vec4(density, 0.0, 0.0, 1.0));
}
